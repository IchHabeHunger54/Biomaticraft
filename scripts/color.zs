#priority 1
import mods.jei.JEI;
val air = <item:minecraft:air>;
val stick = <item:minecraft:stick>;
val planks = <tag:items:minecraft:planks>;
for color in ["black", "blue", "brown", "cyan", "gray", "green", "light_blue", "light_gray", "lime", "magenta", "orange", "pink", "purple", "red", "white", "yellow"] {
	var dye = getTag("forge", "dyes/" + color);
	var wool = getItem("minecraft", color + "_wool");
	bigCompressWithUndo(color + "_petal_block", getItem("botania", color + "_petal"), getItem("botania", color + "_petal_block"));
	cycle(color + "_shingles", getItem("minecraft", color + "_terracotta"), getItem("quark", color + "_shingles"));
	dye1(color + "_elevator", wool, <item:minecraft:ender_pearl>, getItem("elevatorid", "elevator_" + color));
	dye1(color + "_plastic", <item:mekanism:hdpe_pellet>, dye, getItem("mekanismadditions", color + "_plastic"));
	dye8(color + "_cistern", <tag:items:ceramics:porcelain_cisterns>, dye, getItem("ceramics", color + "_porcelain_cistern"));
	dye8(color + "_cobblestone", <tag:items:forge:cobblestone>, dye, getItem("pickletweaks", color + "_cobblestone"));
	dye8(color + "_covered_cable", <item:appliedenergistics2:fluix_covered_cable>, dye, getItem("appliedenergistics2", color + "_covered_cable"));
	dye8(color + "_covered_dense_cable", <item:appliedenergistics2:fluix_covered_dense_cable>, dye, getItem("appliedenergistics2", color + "_covered_dense_cable"));
	dye8(color + "_framed_glass", <item:quark:framed_glass>, dye, getItem("quark", color + "_framed_glass"));
	dye8(color + "_glass", <item:minecraft:glass>, dye, getItem("minecraft", color + "_stained_glass"));
	dye8(color + "_glass_cable", <item:appliedenergistics2:fluix_glass_cable>, dye, getItem("appliedenergistics2", color + "_glass_cable"));
	dye8(color + "_kitchen_floor", <item:minecraft:quartz_block>, getItem("minecraft", color + "_terracotta"), getItem("cookingforblockheads", color + "_kitchen_floor"));
	dye8(color + "_lumen_paint_ball", getItem("appliedenergistics2", color + "_paint_ball"), <item:minecraft:glowstone_dust>, getItem("appliedenergistics2", color + "_lumen_paint_ball"));
	dye8(color + "_paint_ball", <item:appliedenergistics2:matter_ball>, dye, getItem("appliedenergistics2", color + "_paint_ball"));
	dye8(color + "_plastic_glow", getItem("mekanismadditions", color + "_plastic"), <item:minecraft:glowstone_dust>, getItem("mekanismadditions", color + "_plastic_glow"));
	dye8(color + "_plastic_road", getItem("mekanismadditions", color + "_plastic"), <tag:items:minecraft:sand>, getItem("mekanismadditions", color + "_plastic_road"));
	dye8(color + "_plastic_transparent", getItem("mekanismadditions", color + "_plastic"), <tag:items:quark:shards>, getItem("mekanismadditions", color + "_plastic_transparent"));
	dye8(color + "_porcelain", <tag:items:ceramics:porcelain_block>, dye, getItem("ceramics", color + "_porcelain"));
	dye8(color + "_quilted_wool", wool, <item:minecraft:string>, getItem("quark", color + "_quilted_wool"));
	dye8(color + "_reinforced_plastic", getItem("mekanismadditions", color + "_plastic"), <item:mekanism:dust_osmium>, getItem("mekanismadditions", color + "_reinforced_plastic"));
	dye8(color + "_slick_plastic", getItem("mekanismadditions", color + "_plastic"), <tag:items:forge:slimeballs>, getItem("mekanismadditions", color + "_slick_plastic"));
	dye8(color + "_smart_cable", <item:appliedenergistics2:fluix_smart_cable>, dye, getItem("appliedenergistics2", color + "_smart_cable"));
	dye8(color + "_smart_dense_cable", <item:appliedenergistics2:fluix_smart_dense_cable>, dye, getItem("appliedenergistics2", color + "_smart_dense_cable"));
	dye8(color + "_terracotta", <item:minecraft:terracotta>, dye, getItem("minecraft", color + "_terracotta"));
	dye8(color + "_wool", getTag("minecraft", "wool"), dye, wool);
	getItem("inspirations", color + "_carpeted_trapdoor").burnTime = 400;
	getItem("minecraft", color + "_banner").burnTime = 275;
	getItem("minecraft", color + "_bed").burnTime = 1200;
	getItem("minecraft", color + "_carpet").burnTime = 25;
	getItem("quark", color + "_quilted_wool").burnTime = 100;
	JEI.hideItem(getItem("inspirations", color + "_book"));
	JEI.hideItem(getItem("securitycraft", "reinforced_" + color + "_carpet"));
	JEI.hideItem(getItem("securitycraft", "reinforced_" + color + "_concrete"));
	JEI.hideItem(getItem("securitycraft", "reinforced_" + color + "_stained_glass"));
	JEI.hideItem(getItem("securitycraft", "reinforced_" + color + "_stained_glass_pane"));
	JEI.hideItem(getItem("securitycraft", "reinforced_" + color + "_terracotta"));
	JEI.hideItem(getItem("securitycraft", "reinforced_" + color + "_wool"));
	kiln(color + "_concrete", getItem("minecraft", color + "_concrete"), getItem("minecraft", color + "_concrete_powder"));
	kiln(color + "_glazed_terracotta", getItem("minecraft", color + "_terracotta"), getItem("minecraft", color + "_glazed_terracotta"));
	shaped(color + "_banner", getItem("minecraft", color + "_banner"), [[wool], [wool], [stick]]);
	shaped(color + "_bed", getItem("minecraft", color + "_bed"), [[wool, wool, wool], [planks, planks, planks]]);
	shaped(color + "_botany_pot", getItem("botanypots", color + "_botany_pot"), [[getItem("minecraft", color + "_terracotta"), air, getItem("minecraft", color + "_terracotta")], [getItem("minecraft", color + "_terracotta"), getItem("minecraft", color + "_terracotta"), getItem("minecraft", color + "_terracotta")]]);
	shaped(color + "_carpet", getItem("minecraft", color + "_carpet") * 3, [[wool, wool]]);
	shaped(color + "_fence", getItem("mekanismadditions", color + "_plastic_fence") * 4, [[planks, <item:mekanism:hdpe_stick>, planks], [planks, <item:mekanism:hdpe_stick>, planks]]);
	shaped(color + "_framed_glass_pane", getItem("quark", color + "_framed_glass_pane") * 8, [[getItem("quark", color + "_framed_glass"), getItem("quark", color + "_framed_glass")], [getItem("quark", color + "_framed_glass"), getItem("quark", color + "_framed_glass")]]);
	shaped(color + "_gate", getItem("mekanismadditions", color + "_plastic_fence_gate") * 2, [[<item:mekanism:hdpe_stick>, planks, <item:mekanism:hdpe_stick>], [<item:mekanism:hdpe_stick>, planks, <item:mekanism:hdpe_stick>]]);
	shaped(color + "_glass_pane", getItem("minecraft", color + "_stained_glass_pane") * 8, [[getItem("minecraft", color + "_stained_glass"), getItem("minecraft", color + "_stained_glass")], [getItem("minecraft", color + "_stained_glass"), getItem("minecraft", color + "_stained_glass")]]);
	shaped(color + "_stool", getItem("quark", color + "_stool"), [[wool, wool, wool], [<tag:items:minecraft:wooden_slabs>, <tag:items:minecraft:wooden_slabs>, <tag:items:minecraft:wooden_slabs>]]);
	shapeless(color + "_balloon", getItem("mekanismadditions", color + "_balloon"), [dye, <item:minecraft:leather>, <item:minecraft:string>]);
	shapeless(color + "_candle", getItem("quark", color + "_candle"), [dye, <item:minecraft:string>, <item:quark:tallow>]);
	shapeless(color + "_carpeted_trapdoor", getItem("inspirations", color + "_carpeted_trapdoor"), [<tag:items:minecraft:wooden_trapdoors>, getItem("minecraft", color + "_carpet")]);
	shapeless(color + "_concrete_powder", getItem("minecraft", color + "_concrete_powder") * 8, [dye, <item:minecraft:gravel>, <item:minecraft:gravel>, <item:minecraft:gravel>, <item:minecraft:gravel>, <tag:items:minecraft:sand>, <tag:items:minecraft:sand>, <tag:items:minecraft:sand>, <tag:items:minecraft:sand>]);
	shapeless(color + "_dye_petal", getItem("minecraft", color + "_dye"), [getTag("botania", "petals/" + color)]);
	shapeless(color + "_floating_flower", getItem("botania", color + "_floating_flower"), [getItem("botania", color + "_shiny_flower"), <item:minecraft:dirt>, <item:botania:grass_seeds>]);
	shapeless(color + "_glow_panel", getItem("mekanismadditions", color + "_glow_panel"), [dye, <item:mekanism:hdpe_pellet>, <tag:items:forge:glass>, <item:minecraft:glowstone_dust>]);
	shapeless(color + "_hopping_botany_pot", getItem("botanypots", "hopper_" + color + "_botany_pot"), [getItem("botanypots", color + "_botany_pot"), <item:minecraft:hopper>]);
	shapeless(color + "_item_frame", getItem("quark", color + "_item_frame"), [dye, <item:minecraft:item_frame>]);
	shapeless(color + "_mushroom", getItem("botania", color + "_mushroom"), [<tag:items:forge:mushrooms>, dye]);
	shapeless(color + "_petal", getItem("botania", color + "_petal"), [getItem("botania", color + "_mystical_flower")]);
	shapeless(color + "_petal_double", getItem("botania", color + "_petal") * 2, [getItem("botania", color + "_double_flower")]);
	shapeless(color + "_petal_shiny", getItem("botania", color + "_petal"), [getItem("botania", color + "_shiny_flower")]);
	shapeless(color + "_petal_shroom", getItem("botania", color + "_petal"), [getItem("botania", color + "_mushroom")]);
	shapeless(color + "_quilted_wool_rev", wool, [getItem("quark", color + "_quilted_wool")]);
	shapeless(color + "_sharestone", getItem("waystones", color + "_sharestone"), [<tag:items:waystones:sharestone>, dye]);
	shapeless(color + "_shiny_flower", getItem("botania", color + "_shiny_flower"), [getItem("botania", color + "_mystical_flower"), <item:minecraft:glowstone_dust>]);
	shapeless(color + "_shulker_box", getItem("minecraft", color + "_shulker_box"), [dye, <item:minecraft:purple_shulker_box>]);
	slab(color + "_plastic_glow_slab", getItem("mekanismadditions", color + "_plastic_glow_slab"), getItem("mekanismadditions", color + "_plastic_glow"));
	slab(color + "_plastic_slab", getItem("mekanismadditions", color + "_plastic_slab"), getItem("mekanismadditions", color + "_plastic"));
	slab(color + "_plastic_transparent_slab", getItem("mekanismadditions", color + "_plastic_transparent_slab"), getItem("mekanismadditions", color + "_plastic_transparent"));
	slab(color + "_shingles_slab", getItem("quark", color + "_shingles_slab"), getItem("quark", color + "_shingles"));
	smallCompressWithUndo(color + "_shard", getItem("quark", color + "_shard"), getItem("minecraft", color + "_stained_glass"));
	stairs(color + "_plastic_glow_stairs", getItem("mekanismadditions", color + "_plastic_glow_stairs"), getItem("mekanismadditions", color + "_plastic_glow"));
	stairs(color + "_plastic_stairs", getItem("mekanismadditions", color + "_plastic_stairs"), getItem("mekanismadditions", color + "_plastic"));
	stairs(color + "_plastic_transparent_stairs", getItem("mekanismadditions", color + "_plastic_transparent_stairs"), getItem("mekanismadditions", color + "_plastic_transparent"));
	stairs(color + "_shingles_stairs", getItem("quark", color + "_shingles_stairs"), getItem("quark", color + "_shingles"));
	vertSlab(color + "_shingles_vert_slab", getItem("quark", color + "_shingles_vertical_slab"), getItem("quark", color + "_shingles_slab"));
	wool.burnTime = 100;
}
shapeless("black_floppy_disk", <item:computercraft:disk>.withTag({Color: 1118481 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/black>]);
shapeless("blue_floppy_disk", <item:computercraft:disk>.withTag({Color: 3368652 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/blue>]);
shapeless("brown_floppy_disk", <item:computercraft:disk>.withTag({Color: 8349260 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/brown>]);
shapeless("cyan_floppy_disk", <item:computercraft:disk>.withTag({Color: 5020082 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/cyan>]);
shapeless("gray_floppy_disk", <item:computercraft:disk>.withTag({Color: 5000268 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/gray>]);
shapeless("green_floppy_disk", <item:computercraft:disk>.withTag({Color: 5744206 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/green>]);
shapeless("light_blue_floppy_disk", <item:computercraft:disk>.withTag({Color: 10072818 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/light_blue>]);
shapeless("light_gray_floppy_disk", <item:computercraft:disk>.withTag({Color: 10066329 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/light_gray>]);
shapeless("lime_floppy_disk", <item:computercraft:disk>.withTag({Color: 8375321 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/lime>]);
shapeless("magenta_floppy_disk", <item:computercraft:disk>.withTag({Color: 15040472 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/magenta>]);
shapeless("orange_floppy_disk", <item:computercraft:disk>.withTag({Color: 15905331 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/orange>]);
shapeless("pink_floppy_disk", <item:computercraft:disk>.withTag({Color: 15905484 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/pink>]);
shapeless("purple_floppy_disk", <item:computercraft:disk>.withTag({Color: 11691749 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/purple>]);
shapeless("red_floppy_disk", <item:computercraft:disk>.withTag({Color: 13388876 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/red>]);
shapeless("white_floppy_disk", <item:computercraft:disk>.withTag({Color: 15790320 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/white>]);
shapeless("yellow_floppy_disk", <item:computercraft:disk>.withTag({Color: 14605932 as int}), [<item:minecraft:redstone>, <item:minecraft:paper>, <tag:items:forge:dyes/yellow>]);
